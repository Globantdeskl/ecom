buildscript {
	dependencies {
		classpath(
			["org.springframework.boot:spring-boot-gradle-plugin:2.3.8.RELEASE"],
			["org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"],
			["com.google.guava:guava"]
		)
	}
}

plugins {
	id 'org.springframework.boot' version '2.3.8.RELEASE'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'com.google.cloud.tools.jib' version '2.7.1'
}

apply plugin: "org.sonarqube"
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

group = 'com.aeo.checkout.submitorder'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	jib {
		container {
			user '10000:10000'
			jvmFlags = ['-javaagent:/app/libs/newrelic-agent-6.4.2.jar', 
						'-Dnewrelic.config.file=/app/resources/newrelic.yml']
		}
		from {
			image "openjdk:14"
		}
	}
}

repositories {
	maven {
		url "https://artifactory.ae.com/artifactory/libs-release/"
	}
}

ext {
	set('springCloudVersion', "Hoxton.SR9")
	set('newRelicVersion', "6.4.2")
	set('swaggerVersion', "2.9.2")
}

dependencies {

	implementation( 
	
		// java-ee, web, and cloud
		['org.springframework.boot:spring-boot-starter-web'],
		['jakarta.xml.bind:jakarta.xml.bind-api'],
		['org.glassfish.jaxb:jaxb-runtime'],
   		
   		// google pubsub
   		[ 'org.springframework.cloud:spring-cloud-gcp-starter-pubsub' ],
   		[ 'com.google.guava:guava' ],
		[ 'org.springframework.integration:spring-integration-core' ],
   		
   		// resilience
   		['org.springframework.cloud:spring-cloud-starter-netflix-hystrix'],
   		['org.springframework:spring-aspects'],
   		['org.springframework.retry:spring-retry'],
   		
   		// monitoring 
   		['org.springframework.boot:spring-boot-starter-actuator'],
   		['org.springframework.cloud:spring-cloud-starter-sleuth'],
   		["com.newrelic.agent.java:newrelic-api:${newRelicVersion}"],
   		["com.newrelic.agent.java:newrelic-agent:${newRelicVersion}"],
   		
   		// documentation
   		["io.springfox:springfox-swagger2:${swaggerVersion}"],
   		["io.springfox:springfox-swagger-ui:${swaggerVersion}"],
   		
   		// chaos testing
   		["de.codecentric:chaos-monkey-spring-boot:2.3.1"],
   		
   		// logging
   		['com.aeo:logging-spring-boot:1.1.0'],
   		
   		// fulfillment order model
   		[ 'com.aeo.postcheckout:pcs-order-model:1.0.0' ]
	)
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	testImplementation(
		['org.springframework.boot:spring-boot-starter-test'],
		['org.junit.jupiter:junit-jupiter'],
		['org.mockito:mockito-junit-jupiter']
	)
	
	testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
	testLogging {
		exceptionFormat = 'full'
	}
	afterSuite { desc, result ->
		if (!desc.parent) {
			println "Results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
			boolean skipTests = Boolean.parseBoolean(project.findProperty('SKIP_TESTS') ?: "false")
			if (result.testCount == 0 && !skipTests) {
				throw new IllegalStateException("No tests were found. Failing the build")
			}
		}
	}
	finalizedBy jacocoTestReport
}

sonarqube {
	properties {
		property "sonar.projectKey", "MSAT-SUBMIT-ORDER"
		property "sonar.projectName", "Checkout Submit Order Microservices Application"
		property "sonar.projectVersion", "1.0"
		property "sonar.sourceEncoding", "iso-8859-1"
		property "sonar.coverage.exclusions", "**/model/*,**/model/**,**/config/*,**/config/**,**/*Application.java"
		property "sonar.sources","src/main/java"
		property "sonar.exclusions", "src/test/java/**/*"
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
		property "sonar.login","admin"
		property "sonar.password","admin"
	 }
}

jacocoTestReport {
	reports {
		xml.enabled true
		csv.enabled false
		html.destination file("${buildDir}/reports/code-coverage")
	}
}
